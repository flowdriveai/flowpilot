plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['java']
        }
        resources {
            srcDirs = ['resources']
        }
    }
}

dependencies {
    implementation files("$rootProject.projectDir/third_party/opencv-nolibs-4.5.1-2.jar")
    if (System.getenv('USE_CUDA') != null)
        implementation 'com.microsoft.onnxruntime:onnxruntime_gpu:1.4.0'
    else
        implementation files("$rootProject.projectDir/third_party/onnxruntime-stripped-1.7.0.jar")
    implementation files("$rootProject.projectDir/third_party/jocl-2.0.4.jar")
    implementation ("org.nd4j:nd4j-native-platform:$rootProject.nd4j_version")
    {
        exclude ((HashMap) [group: 'org.bytedeco', module: 'openblas-platform'])
        exclude ((HashMap) [group: 'org.bytedeco', module: 'mkl-platform'])
        exclude ((HashMap) [group: 'org.nd4j', module: 'nd4j-native'])
    }
    implementation "org.bytedeco:javacpp:$rootProject.javacpp_version"
    implementation "org.bytedeco:javacpp:$rootProject.javacpp_version:$rootProject.javacpp_platform_suffix"
    implementation "org.nd4j:nd4j-native:$rootProject.nd4j_version"
    implementation "org.nd4j:nd4j-native:$rootProject.nd4j_version:$rootProject.javacpp_platform_suffix"
    implementation "org.bytedeco:openblas:$rootProject.openblas_version-$rootProject.javacpp_version"
    implementation "org.bytedeco:openblas:$rootProject.openblas_version-$rootProject.javacpp_version:$rootProject.javacpp_platform_suffix"
    implementation 'org.capnproto:runtime:0.1.13'
    implementation 'org.yaml:snakeyaml:1.18:android'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation project(":messaging")
    implementation project(':definitions')
    implementation project(":sensord")
    implementation project(":common")
    testImplementation 'junit:junit:4.11'
}


tasks.register('list') {
    dependsOn configurations.compileClasspath
    doLast {
        println "${configurations.compileClasspath.collect { File file -> file.path }.join(' ')}"
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
}

group = 'ai.flow.app'
version = '0.0.1'
description = 'vision-app'


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
